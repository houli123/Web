# 三点渐变
通过径向渐变和极坐标渐变的原理，还可以实现三点渐变。

## 1.建立三个点
```gl
//点1
vec2 p1=vec2(200,200);
vec4 c1=vec4(1,0,0,1);
//点2
vec2 p2=vec2(800,400);
vec4 c2=vec4(0,1,0,1);
//点3
vec2 p3=vec2(400,800);
vec4 c3=vec4(0,0,1,1);
```
p1是点位，c1是颜色。


## 2.基于三个位置点计算相应向量
```gl
vec2 v31=p1-p3;
vec2 v32=p2-p3;
vec2 v12=p2-p1;
```

## 3.提前算出p2和p1点位的色差
```gl
vec4 c12=c2-c1;
```

## 4.提前算出一圈的弧度，以备后用
```gl
float pi2=radians(360.0);
```
radians() 是将角度转弧度的方法，360°=π*2

## 5.建立基于向量获取弧度的方法
```gl
float getAngle(vec2 v){
    float ang=atan(v.y,v.x);
    if(ang<0.0){
        ang+=pi2;
    }
    return ang;
}
```
atan() 方法可以计算一个点基于x轴正方向的弧度，此弧度的取值范围是[-π,π]，为了方便计算，我将其范围设置为[0,2π]

## 6.获取点p1、p2和当前片元位相对于p3点的弧度。
```gl
float ang31=getAngle(v31);
float ang32=getAngle(v32);
vec2 v3f=gl_FragCoord.xy-p3;
float ang3f=getAngle(v3f);
```

## 7.用叉乘计算当前片元在向量v12的哪一侧
```gl
vec2 v1f=gl_FragCoord.xy-p1;
float z=v1f.x*v12.y-v1f.y*v12.x;
```
其实我们还可以这么写
```gl
vec2 v1f=gl_FragCoord.xy-p1;
float z =cross(vec3(v1f,0),vec3(v12,0)).z;
```
实际上就是计算了两个z值为0的三维向量的叉乘结果的z值。

## 8.当片元在向量v31和v32之间，并且当前片元在向量v12的左侧时，当前片元在△p1p2p3中，我们便可以计算片元颜色。
```gl
vec4 color=vec4(0);
if(ang3f>=ang31&&ang3f<=ang32&&z<0.0){
    //计算∠<v3f,p3p1>在∠<p3p2,p3p1>中的比值
    ang3f=clamp(ang3f,ang31,ang32);
    float angRatio=(ang3f-ang31)/(ang32-ang31);

    //基于angRatio计算向量v12和向量v1f的交点位置p4和颜色c4
    vec2 p4=p1+v12*angRatio;
    vec4 c4=c1+c12*angRatio;

    //向量p3-gl_FragCoord在向量p3p4中的长度比
    float lenE=distance(p4,p3);
    float lenF=length(v3f);
    float lenRatio=lenF/lenE;

    //基于长度比获取当前片元在c3、c4间的颜色
    color=c3+(c4-c3)*lenRatio;
}
//片元颜色
gl_FragColor=color;
```

